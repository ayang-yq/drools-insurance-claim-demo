package rules

import com.example.drools.model.Claim
import com.example.drools.model.Policy
import com.example.drools.model.Discrepancy
import com.example.drools.model.ClaimHistory

// 1. Validation Rules (Highest Salience)

rule "Reject if policy is not active"
    salience 100
    when
        $policy : Policy(active == false)
        $claim : Claim(policyId == $policy.id, status == "PENDING")
    then

        modify($claim) {
            setStatus("REJECTED"),
            setComment("Policy is inactive")
        }
        System.out.println("Rule fired: " + drools.getRule().getName());
end

rule "Reject if policy limit exceeded"
    salience 95
    when
        $policy : Policy()
        $claim : Claim(policyId == $policy.id, amount > $policy.limit, status == "PENDING")
    then

        modify($claim) {
            setStatus("REJECTED"),
            setComment("Policy limit exceeded")
        }
        System.out.println("Rule fired: " + drools.getRule().getName());
end

rule "Reject if amount less than deductible"
    salience 90
    when
        $policy : Policy()
        $claim : Claim(policyId == $policy.id, amount < $policy.deductible, status == "PENDING")
    then

        modify($claim) {
            setStatus("REJECTED"),
            setComment("Claim amount is less than deductible")
        }
        System.out.println("Rule fired: " + drools.getRule().getName());
        System.out.println("Rule fired: " + drools.getRule().getName());
end

rule "Reject Travel Claim without Ticket"
    salience 90
    when
        $policy : Policy(type == "Travel")
        $claim : Claim(policyId == $policy.id, amount > 100, status == "PENDING", !providedDocuments.contains("Ticket") && !providedDocuments.contains("Boarding Pass"))
    then
        modify($claim) {
            setStatus("REJECTED"),
            setComment("Travel claim > 100 requires Ticket or Boarding Pass")
        }
        System.out.println("Rule fired: " + drools.getRule().getName());
end

rule "Reject Motor Claim without Police Report"
    salience 90
    when
        $policy : Policy(type == "Motor")
        $claim : Claim(policyId == $policy.id, amount > 2000, status == "PENDING", !providedDocuments.contains("Police Report"))
    then
        modify($claim) {
            setStatus("REJECTED"),
            setComment("Motor claim > 2000 requires Police Report")
        }
        System.out.println("Rule fired: " + drools.getRule().getName());
end

rule "Reject Home Theft Claim without Police Report"
    salience 90
    when
        $policy : Policy(type == "Home")
        $claim : Claim(policyId == $policy.id, claimType == "Theft", status == "PENDING", !providedDocuments.contains("Police Report"))
    then
        modify($claim) {
            setStatus("REJECTED"),
            setComment("Home Theft claim requires Police Report")
        }
        System.out.println("Rule fired: " + drools.getRule().getName());
end

rule "Reject Medical Claim without Invoice or Treatment Record"
    salience 90
    when
        $claim : Claim(claimType == "Medical", status == "PENDING", !providedDocuments.contains("Invoice") || !providedDocuments.contains("Treatment Record"))
    then
        modify($claim) {
            setStatus("REJECTED"),
            setComment("Medical claim requires both Invoice and Treatment Record")
        }
        System.out.println("Rule fired: " + drools.getRule().getName());
end

rule "Flag discrepancy"
    salience 80
    when
        $claim : Claim(status == "PENDING")
        $d : Discrepancy()
    then
        modify($claim) {
            setStatus("INVESTIGATION_REQUIRED"),
            setComment("Discrepancy found: " + $d.getFieldName() + " mismatch. Submitted: " + $d.getSubmittedValue() + ", Evidence: " + $d.getEvidenceValue())
        }
        System.out.println("Rule fired: " + drools.getRule().getName());
end

// 2. Risk Calculation Rules (Medium Salience)
// These rules can fire multiple times and accumulate risk

rule "High risk for large claims"
    salience 200
    when

        $claim : Claim(amount > 10000, status == "PENDING", !firedRules.contains("High risk for large claims"))
    then
        $claim.getFiredRules().add("High risk for large claims");
        modify($claim) {
            setRiskScore($claim.getRiskScore() + 50)
        }
        System.out.println("Rule fired: " + drools.getRule().getName() + " - Risk +50");
end

rule "Risk for frequent claimant"
    salience 200
    when
        $history : ClaimHistory(claimCountLast3Years > 3)
        $claim : Claim(status == "PENDING", !firedRules.contains("Risk for frequent claimant"))
    then
        $claim.getFiredRules().add("Risk for frequent claimant");
        modify($claim) {
            setRiskScore($claim.getRiskScore() + 30)
        }
        System.out.println("Rule fired: " + drools.getRule().getName() + " - Risk +30");
end

rule "Risk for Theft claims"
    salience 200
    when

        $claim : Claim(claimType == "Theft", status == "PENDING", !firedRules.contains("Risk for Theft claims"))
    then
        $claim.getFiredRules().add("Risk for Theft claims");
        modify($claim) {
            setRiskScore($claim.getRiskScore() + 20)
        }
        System.out.println("Rule fired: " + drools.getRule().getName() + " - Risk +20");
end

rule "Risk for suspicious keywords"
    salience 200
    when
        $claim : Claim(comment matches ".*(lost|stolen|unexplained).*", status == "PENDING", !firedRules.contains("Risk for suspicious keywords"))
    then
        $claim.getFiredRules().add("Risk for suspicious keywords");
        modify($claim) {
            setRiskScore($claim.getRiskScore() + 40)
        }
        System.out.println("Rule fired: " + drools.getRule().getName() + " - Risk +40");
end

// 3. Decision Rules (Lowest Salience)

rule "Approve low risk claims"
    salience 10
    when
        $claim : Claim(status == "PENDING", riskScore <= 20)
    then
        $claim.setStatus("APPROVED");
        $claim.setComment("Auto-approved low risk claim");
        System.out.println("Rule fired: " + drools.getRule().getName());
end

rule "Require investigation for high risk claims"
    salience 10
    when
        $claim : Claim(status == "PENDING", riskScore > 20)
    then
        $claim.setStatus("INVESTIGATION_REQUIRED");
        $claim.setComment("High risk score: " + $claim.getRiskScore());
        System.out.println("Rule fired: " + drools.getRule().getName());
end
